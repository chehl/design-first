plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.6'
    id 'io.spring.dependency-management' version '1.1.0'
    id "org.openapi.generator" version "6.5.0"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

group = 'cde.chameleon'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

openApiGenerate {
    // see https://openapi-generator.tech/docs/generators/spring/
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/api/locations.yml"
    outputDir = "$buildDir/generated/sources/openapi"
    invokerPackage ="cde.designfirst"
    apiPackage = "cde.designfirst.locations.api"
    modelPackage = "cde.designfirst.locations.api.dto"
    configOptions = [
            useSpringBoot3: "true",
            openApiNullable: "false",
            delegatePattern: "true",
            hideGenerationTimestamp: "true",
            useTags: "true"
    ]
}

sourceSets {
    main {
        java.srcDirs += "$buildDir/generated/sources/openapi/src/main/java"
    }
}

dependencies {
    // starters
    implementation 'org.springframework.boot:spring-boot-starter-web:3.0.6'
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.0.6'

    // prod dependencies
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.0'

    // dev tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools:3.0.6'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.springframework.boot:spring-boot-test:1.4.0.RELEASE'
}

test {
    useJUnitPlatform()
}